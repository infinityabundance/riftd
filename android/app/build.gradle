plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "com.example.riftmobile"
    compileSdk 34

    defaultConfig {
        applicationId "com.example.riftmobile"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "0.1.0"
        ndk {
            abiFilters "arm64-v8a", "x86_64"
        }
    }

    buildFeatures {
        compose true
    }
    ndkVersion "26.1.10909125"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.8"
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

def jniLibDir = file("src/main/jniLibs/arm64-v8a")
def riftSo = file("${jniLibDir}/librift_sdk.so")
def appJniLibsDir = file("src/main/jniLibs")
def ndkLibDir = file("${android.ndkDirectory}/toolchains/llvm/prebuilt/linux-x86_64/sysroot/usr/lib")
def stlLibDir = file("${ndkLibDir}/x86_64-linux-android")
def stlLibDirArm64 = file("${ndkLibDir}/aarch64-linux-android")
def stlTargetDir = file("${appJniLibsDir}/x86_64")
def stlTargetDirArm64 = file("${appJniLibsDir}/arm64-v8a")

tasks.register("buildRust", Exec) {
    workingDir rootProject.rootDir
    commandLine "/bin/sh", "${rootProject.rootDir}/build-rust-android.sh"
}

tasks.register("copyCxxSharedLibX86_64", Copy) {
    from fileTree(stlLibDir) { include "libc++_shared.so" }
    into stlTargetDir
}

tasks.register("copyCxxSharedLibArm64", Copy) {
    from fileTree(stlLibDirArm64) { include "libc++_shared.so" }
    into stlTargetDirArm64
}

preBuild.dependsOn(tasks.named("copyCxxSharedLibX86_64"))
preBuild.dependsOn(tasks.named("copyCxxSharedLibArm64"))
preBuild.dependsOn(tasks.named("buildRust"))

dependencies {
    implementation platform("androidx.compose:compose-bom:2024.04.01")
    implementation "androidx.activity:activity-compose:1.9.0"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0"
    debugImplementation "androidx.compose.ui:ui-tooling"
}
